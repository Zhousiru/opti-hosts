// ```
// # OPTI-HOSTS <Domain> [<Location> * <Limit>, <Location>, ...]
// ```

WHITESPACE = _{ " " }

domain_char            = { ASCII_ALPHANUMERIC | "_" | "-" | "." }
location_char_no_space = { ASCII_ALPHANUMERIC | "_" | "-" | "." | "(" | ")" | "&" }

domain   = @{ domain_char+ }
location = @{
    location_char_no_space+ ~ (" "+ ~ location_char_no_space+)*
}
limit    = @{ ASCII_DIGIT+ }

location_limit_item = !{
    location ~ "*" ~ limit
  | location
}

location_limit_array = !{
    "[" ~ location_limit_item ~ ("," ~ location_limit_item)* ~ "]"
}

expression = ${ domain ~ " "+ ~ location_limit_array }

prefix = ${ "OPTI-HOSTS" ~ &" " }

directive = { SOI ~ "#" ~ prefix ~ expression ~ EOI }
